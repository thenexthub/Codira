add_library(languageCodiraOnoneSupport
  CodiraOnoneSupport.code
  "${PROJECT_SOURCE_DIR}/linker-support/magic-symbols-for-install-name.c")

set_target_properties(languageCodiraOnoneSupport PROPERTIES
 Codira_MODULE_NAME CodiraOnoneSupport)

if(APPLE AND BUILD_SHARED_LIBS)
  target_link_options(languageCodiraOnoneSupport PRIVATE "SHELL:-Xlinker -headerpad_max_install_names")
endif()

target_compile_options(languageCodiraOnoneSupport PRIVATE
  $<$<COMPILE_LANGUAGE:Codira>:-parse-stdlib>
  "$<$<COMPILE_LANGUAGE:Codira>:SHELL:-Xtoolchain -sil-inline-generics=false>"

  # We have to disable validation of TBD files, because this module is
  # _explicitly_ special-cased to result in extra symbols generated by the
  # optimizer, meaning TBDGen can't (and shouldn't: it has to run
  # pre-optimization for performance) list them.
  # See also caa3dd4d291ec93c1a59f1db62604e703bff8468
  "$<$<COMPILE_LANGUAGE:Codira>:SHELL:-Xfrontend -validate-tbd-against-ir=none>"

  "$<$<COMPILE_LANGUAGE:Codira>:SHELL:-Xfrontend -check-onone-completeness>"
  "$<$<COMPILE_LANGUAGE:Codira>:SHELL:-Xfrontend -disable-access-control>")

target_compile_definitions(languageCodiraOnoneSupport PRIVATE
  $<$<COMPILE_LANGUAGE:C,CXX>:-DLANGUAGE_TARGET_LIBRARY_NAME=languageCodiraOnoneSupport>)

target_link_libraries(languageCodiraOnoneSupport
  PRIVATE
    languageShims
    languageCore)

install(TARGETS languageCodiraOnoneSupport
    EXPORT CodiraCoreTargets
    COMPONENT CodiraCore_runtime
    ARCHIVE DESTINATION "${CodiraCore_INSTALL_LIBDIR}"
    LIBRARY DESTINATION "${CodiraCore_INSTALL_LIBDIR}"
    RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}")
emit_language_interface(languageCodiraOnoneSupport)
install_language_interface(languageCodiraOnoneSupport)

# Configure plist creation for Darwin platforms.
generate_plist("${CMAKE_PROJECT_NAME}" "${CMAKE_PROJECT_VERSION}" languageCodiraOnoneSupport)
