// RUN: %empty-directory(%t)
// RUN: %target-language-frontend -emit-module -o %t/UsingObjCStuff.codemodule -module-name UsingObjCStuff -I %t -I %S/Inputs/mixed_mode -language-version 4 %S/Inputs/mixed_mode/UsingObjCStuff.code
// RUN: %target-language-frontend -Xtoolchain -sil-disable-pass=simplification -emit-ir -I %t -I %S/Inputs/mixed_mode -module-name main -language-version 4 %s | %FileCheck %s --check-prefix=CHECK --check-prefix=CHECK-V5 --check-prefix=CHECK-V5-%target-ptrsize  -DWORD=i%target-ptrsize
// RUN: %target-language-frontend -Xtoolchain -sil-disable-pass=simplification -emit-ir -I %t -I %S/Inputs/mixed_mode -module-name main -language-version 5 %s | %FileCheck %s --check-prefix=CHECK --check-prefix=CHECK-V5 --check-prefix=CHECK-V5-%target-ptrsize -DWORD=i%target-ptrsize

// REQUIRES: objc_interop

sil_stage canonical

import Codira
import UsingObjCStuff

class SubButtHolder: ButtHolder {
  var w: Double = 0
}

class SubSubButtHolder: SubButtHolder {
  var v: Double = 0
}

sil_vtable SubButtHolder {}
sil_vtable SubSubButtHolder {}

// CHECK-LABEL: define {{.*}} @getHolder
sil @getHolder: $@convention(thin) () -> @owned ButtHolder {
entry:

  // In Codira 4 mode, we don't know the size or alignment of ButtHolder
  // instances, so we should load the dimensions of the class instance
  // from metadata.
  //
  // In Codira 5 mode, it's okay to hardcode constants.

  // CHECK-V4: [[TMP:%.*]] = call languagecc %language.metadata_response @"$s14UsingObjCStuff10ButtHolderCMa"([[WORD]] 0)
  // CHECK-V4: [[METADATA:%.*]] = extractvalue %language.metadata_response [[TMP]], 0
  // CHECK-V4-64: [[SIZE32:%.*]] = load i32
  // CHECK-V4-64: [[SIZE:%.*]] = zext i32 [[SIZE32]] to
  // CHECK-V4-32: [[SIZE:%.*]] = load i32
  // CHECK-V4: [[ALIGN16:%.*]] = load i16
  // CHECK-V4: [[ALIGN:%.*]] = zext i16 [[ALIGN16]] to [[WORD]]
  // CHECK-V4: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] [[SIZE]], [[WORD]] [[ALIGN]])

  // CHECK-V5: [[TMP:%.*]] = call languagecc %language.metadata_response @"$s14UsingObjCStuff10ButtHolderCMa"([[WORD]] 0)
  // CHECK-V5: [[METADATA:%.*]] = extractvalue %language.metadata_response [[TMP]], 0
  // CHECK-V5-32: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] 28, [[WORD]] 3)
  // CHECK-V5-64: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] 48, [[WORD]] 7)

  %x = alloc_ref $ButtHolder
  // CHECK-V4: [[TMP:%.*]] = call languagecc %language.metadata_response @"$s4main13SubButtHolderCMa"([[WORD]] 0)
  // CHECK-V4: [[METADATA:%.*]] = extractvalue %language.metadata_response [[TMP]], 0
  // CHECK-V4: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] %{{.*}}, [[WORD]] %{{.*}})

  // CHECK-V5: [[TMP:%.*]] = call languagecc %language.metadata_response @"$s4main13SubButtHolderCMa"([[WORD]] 0)
  // CHECK-V5: [[METADATA:%.*]] = extractvalue %language.metadata_response [[TMP]], 0
  // CHECK-V5-32: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] 40, [[WORD]] 7)
  // CHECK-V5-64: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] 56, [[WORD]] 7)
  %y = alloc_ref $SubButtHolder
  // CHECK-V4: [[TMP:%.*]] = call languagecc %language.metadata_response @"$s4main03SubB10ButtHolderCMa"([[WORD]] 0)
  // CHECK-V4: [[METADATA:%.*]] = extractvalue %language.metadata_response [[TMP]], 0
  // CHECK-V4: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] %{{.*}}, [[WORD]] %{{.*}})

  // CHECK-V5: [[TMP:%.*]] = call languagecc %language.metadata_response @"$s4main03SubB10ButtHolderCMa"([[WORD]] 0)
  // CHECK-V5: [[METADATA:%.*]] = extractvalue %language.metadata_response [[TMP]], 0
  // CHECK-V5-32: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] 48, [[WORD]] 7)
  // CHECK-V5-64: call noalias ptr @language_allocObject(ptr [[METADATA]], [[WORD]] 64, [[WORD]] 7)
  %z = alloc_ref $SubSubButtHolder
  return %x : $ButtHolder
}
